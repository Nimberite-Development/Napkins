PlayerInfoUpdateActions -> Enum[SInt8]:
  {0x01} AddPlayer, {0x02} InitializeChat, {0x04} UpdateGameMode, {0x08} UpdateListed,
  {0x10} UpdateLatency, {0x20} UpdateDisplayName

Property -> Struct:
  name: String[32767]
  value: String[32767]
  isSigned: Bool
  @ isSigned:
    signature: String[32767]

PlayerAction -> Struct[actions: SInt8]:
  @ actions & PlayerInfoUpdateActions.AddPlayer:
    name: String(16)
    propertyCount: VInt32
    properties: Array[propertyCount, Property]

  @ actions & PlayerInfoUpdateActions.InitializeChat:
    hasSignatureData: Bool
    @ hasSignatureData:
      chatSessionId: UUID
      publicKeyExpiryTime: SInt64
      encodedPublicKeySize: ConstrainU[VInt32, 512]
      encodedPublicKey: Array[encodedPublicKeySize, UInt8]
      publicKeySignatureSize: ConstrainU[VInt32, 4096]
      publicKeySignature: Array[publicKeySignatureSize, UInt8]

  @ actions & PlayerInfoUpdateActions.UpdateGameMode:
    gameMode: VInt32

  @ actions & PlayerInfoUpdateActions.UpdateListed:
    listed: Bool

  @ actions & PlayerInfoUpdateActions.UpdateLatency:
    ping: VInt32

  @ actions & PlayerInfoUpdateActions.UpdateDisplayName:
    hasDisplayName: Bool
    @ hasDisplayName:
      displayName: NBTTextComponent

PlayerInfo -> Struct[actions: SInt8]:
  uuid: UUID
  action: PlayerAction[actions]

PlayerInfoUpdate(0, 0x3C) -> Client:
  actions: SInt8
  playerCount: VInt32
  playerActions: Array[playerCount, PlayerInfo[actions]]